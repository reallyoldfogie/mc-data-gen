plugins {
    id "fabric-loom" version "1.14-SNAPSHOT"
    id "java"
}

version = "1.0.0"
group = "com.example.dataexporter"

repositories {
    mavenCentral()
    maven {
        name = "Fabric"
        url = "https://maven.fabricmc.net/"
    }
}

def minecraftVersion = project.properties["minecraft_version"]
def yarnMappings     = project.properties["yarn_mappings"]
def loaderVersion    = project.properties["loader_version"]
def fabricApiVersion = project.properties["fabric_api_version"]

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    
    // Only use Yarn mappings if provided (versions < 26.1)
    // Versions >= 26.1 ship with non-obfuscated code
    if (yarnMappings != null && !yarnMappings.isEmpty()) {
        mappings "net.fabricmc:yarn:${yarnMappings}:v2"
    }
    
    modImplementation "net.fabricmc:fabric-loader:${loaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricApiVersion}"
}

loom {
    runs {
        server {
            server()
            name "Data Exporter Server"
            // vmArgs "-Dfabric-api.datagen=true", "-Dfabric-api.datagen.output-dir=build/datagen"
        }
    }
}

java {
    toolchain {
        // Minecraft 26.1+ requires Java 25, older versions use Java 21
        def javaVersion = 21
        if (minecraftVersion != null) {
            def versionParts = minecraftVersion.tokenize('.')
            if (versionParts.size() >= 1) {
                def major = versionParts[0].toInteger()
                if (major >= 26) {
                    javaVersion = 25
                }
            }
        }
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}
